// ######################################## SHADER BASE ########################################

#include "basic_util.hlsl"
#include "indexing.hlsl"
#include "logging.hlsl"

uint3 id__;
uint2 SubgridGroupSize;
uint2 SubgridCurrentThreadID;

uint2 id()
{
    uint2 subgrid_id = uint2(id__.x, id__.y);
    return subgrid_id * SubgridGroupSize + SubgridCurrentThreadID;
}

int2 CalculationPosition;
uint2 CalculationSize;

RWStructuredBuffer<float4> Debug;
RWStructuredBuffer<DebugListInfo> DebugInfo;

void set_debug_list_info(uint size, uint t)
{
    DebugListInfo dli;
    dli.Size = size;
    dli.Type = t;
    DebugInfo[0] = dli;
}

void log_(uint index, float4 f)
{
    Debug[XYZ(id().x, id().y, index, CalculationSize)] = f;
}

// ###################################### SHADER BASE END ######################################

#include "convert.hlsl"
#include "volume.hlsl"
#include "paint.hlsl"
#include "math.hlsl"
#include "sutherland_hodgman.hlsl"

uint TextureResolution;

float3 RakelAnchor;
float RakelRotation;
float RakelTilt;
RWStructuredBuffer<Paint> RakelApplicationReservoir;
RWStructuredBuffer<Paint> RakelPickupReservoir;
uint2 RakelReservoirSize;

RWStructuredBuffer<Paint> CanvasReservoir;
uint2 CanvasReservoirSize;

int ClipRadiusX;
float RakelTilt_MAX;
float EmitDistance_MAX;
float EmitVolume_MIN;
float EmitVolume_MAX;
float EmitVolumeApplicationReservoirRate;
float EmitVolumePickupReservoirRate;

RWStructuredBuffer<float> DistortionMap;
uint2 DistortionMapSize;
uint DistortionMapIndex;

RWStructuredBuffer<Paint> RakelEmittedPaint;


bool pixel_in_reservoir_range(int2 pixel)
{
    return pixel.x >= 0
        && pixel.x < (int)RakelReservoirSize.x
        && pixel.y >= 0
        && pixel.y < (int)RakelReservoirSize.y;
}

// radius of 1 does an emit on a 3x3 area from emit source grid, based on the rounded rakel_reservoir_pixel
void emit(float2 rakel_reservoir_pixel, int2 radius, float distance, int2 canvas_pixel)
{
    int2 rakel_reservoir_pixel_nearest = round(rakel_reservoir_pixel);

    // TODO For more detail, this (and the volume_to_emit value) could also be calculated
    //      based on the polygon overlap
    float volume_rakel =
        RakelApplicationReservoir[XYZ(rakel_reservoir_pixel_nearest.x, rakel_reservoir_pixel_nearest.y, 1, RakelReservoirSize)].volume
        + RakelPickupReservoir[XYZ(rakel_reservoir_pixel_nearest.x, rakel_reservoir_pixel_nearest.y, 1, RakelReservoirSize)].volume;
    float volume_canvas = CanvasReservoir[XYZ(canvas_pixel.x, canvas_pixel.y, 1, CanvasReservoirSize)].volume;
    float paint_thickness_rakel = volume_rakel * VOLUME_THICKNESS();
    float paint_thickness_canvas = volume_canvas * VOLUME_THICKNESS();
    float surface_distance = distance - paint_thickness_rakel - paint_thickness_canvas;
    bool surfaces_touch = surface_distance <= 0;

    if (surfaces_touch)
    {
        float volume_to_emit = emit_volume(
            distance,
            EmitDistance_MAX,
            RakelTilt,
            RakelTilt_MAX,
            EmitVolume_MIN * PAINT_UNIT(),
            EmitVolume_MAX * PAINT_UNIT());

        //TODO there might be problems with rounding to negative indizes
        // VOLUME_TO_EMIT_TARGET_PU *= DistortionMap[XY(rakel_reservoir_pixel_nearest.x + DistortionMapIndex, rakel_reservoir_pixel_nearest.y, DistortionMapSize.x)];

        // Array size should really be [1+2*radius.y][1+2*radius.x] but we can't make dynamic arrays
        // in HLSL. So we do 9x9, which is enough for 79 degree tilt and more is not useful for emitting anyways.
        // Also, with 11x11 we would get the following warning:
        // Shader warning in 'EmitFromRakel': Program 'main', warning X4714: sum of temp registers and indexable temp registers times 32 threads exceeds the recommended total 16384.  Performance may be reduced at kernel main (on metal)
        float4 color_to_emit_app[9][9];
        float4 color_to_emit_pu[9][9];
        float volume_to_emit_app[9][9];
        float volume_to_emit_pu[9][9];
        float4 result_color_app = float4(0,0,0,0);
        float4 result_color_pu = float4(0,0,0,0);
        float result_volume_app = 0;
        float result_volume_pu = 0;
        int y, x; // declare loop variables here so the compiler won't complain about double declaration of y
        for (y=-radius.y; y<=radius.y; y++) {
            for (x=-radius.x; x<=radius.x; x++) {
                // initialize arrays
                uint i = y + radius.y;
                uint j = x + radius.x;
                volume_to_emit_app[i][j] = 0;
                volume_to_emit_pu[i][j] = 0;
                color_to_emit_app[i][j] = float4(0,0,0,0);
                color_to_emit_pu[i][j] = float4(0,0,0,0);

                // calculate part
                int2 coord = rakel_reservoir_pixel_nearest + int2(x, y);
                if (pixel_in_reservoir_range(coord)) { // Prevent out of bounds access to reservoir. Also, if it's not in range, this overlap part is not (and must not be!) used anyways.
                    float2 rakel_anchor_index_space = rakel_anchor_to_index_space(RakelAnchor, TextureResolution);
                    float2 rect_rakel_reservoir_pixel = rotate_by_y_2D(rakel_reservoir_pixel, RakelTilt, rakel_anchor_index_space);
                    float2 rect_coord = rotate_by_y_2D(coord, RakelTilt, rakel_anchor_index_space);
                    Rectangle fragment_rectangle = create_rectangle(rect_rakel_reservoir_pixel, -RakelRotation, 0);
                    Rectangle reservoir_rectangle = create_rectangle(rect_coord, 0, RakelTilt);
                    float overlap = calculate_exact_overlap(fragment_rectangle, reservoir_rectangle);

                    if (overlap > 0) {
                        // get paint
                        uint2 coord_ = uint2(coord.x, coord.y);
                        Paint available_app = RakelApplicationReservoir[XYZ(coord_.x, coord_.y, 1, RakelReservoirSize)];
                        Paint available_pu = RakelPickupReservoir[XYZ(coord_.x, coord_.y, 1, RakelReservoirSize)];

                        // calculate volume
                        // .. prefer pickup reservoir with rate
                        float pu_reservoir_rate = EmitVolumePickupReservoirRate / (EmitVolumeApplicationReservoirRate + EmitVolumePickupReservoirRate);
                        float target_volume_pu = overlap * volume_to_emit * pu_reservoir_rate;
                        float really_available_volume_pu = overlap * available_pu.volume;
                        // .. take rest from application reservoir
                        float target_volume_app = overlap * volume_to_emit - really_available_volume_pu;
                        float really_available_volume_app = overlap * available_app.volume;
                        // .. if this is not enough, try take rest from pickup reservoir
                        float app_diff = target_volume_app - really_available_volume_app;
                        if (app_diff > 0)
                        {
                            target_volume_pu += app_diff;
                        }

                        volume_to_emit_app[i][j] = min(target_volume_app, really_available_volume_app);
                        volume_to_emit_pu[i][j] = min(target_volume_pu, really_available_volume_pu);
                        result_volume_app += volume_to_emit_app[i][j];
                        result_volume_pu += volume_to_emit_pu[i][j];

                        color_to_emit_app[i][j] = available_app.color;
                        color_to_emit_pu[i][j] = available_pu.color;
                    }
                }
            }
        }

        // determine color based on emitted volumes
        // TODO use mix function
        for (y=-radius.y; y<=radius.y; y++) {
            for (x=-radius.x; x<=radius.x; x++) {
                int2 coord = rakel_reservoir_pixel_nearest + int2(x, y);
                if (pixel_in_reservoir_range(coord)) {
                    uint i = y + radius.y;
                    uint j = x + radius.x;
                    float volume_part_app = volume_to_emit_app[i][j] / unzero(result_volume_app);
                    float volume_part_pu = volume_to_emit_pu[i][j] / unzero(result_volume_pu);
                    result_color_app += volume_part_app * color_to_emit_app[i][j];
                    result_color_pu += volume_part_pu * color_to_emit_pu[i][j];
                }
            }
        }

        Paint result_paint_app;
        Paint result_paint_pu;
        result_paint_app.color = result_color_app;
        result_paint_pu.color = result_color_pu;
        result_paint_app.volume = result_volume_app;
        result_paint_pu.volume = result_volume_pu;

        Paint result_paint = mix(result_paint_app, result_paint_pu);

        // delete amount of taken volume from reservoir (z=0)
        float deleted = 0;
        for (y=-radius.y; y<=radius.y; y++) {
            for (x=-radius.x; x<=radius.x; x++) {
                int2 coord = rakel_reservoir_pixel_nearest + int2(x, y);
                if (pixel_in_reservoir_range(coord)) {
                    uint i = y + radius.y;
                    uint j = x + radius.x;
                    float to_be_deleted_app = volume_to_emit_app[i][j];
                    float to_be_deleted_pu = volume_to_emit_pu[i][j];

                    uint2 coord_ = uint2(coord.x, coord.y);
                    RakelApplicationReservoir[XYZ(coord_.x, coord_.y, 0, RakelReservoirSize)].volume -= to_be_deleted_app;
                    deleted += to_be_deleted_app;
                    RakelPickupReservoir[XYZ(coord_.x, coord_.y, 0, RakelReservoirSize)].volume -= to_be_deleted_pu;
                }
            }
        }

        RakelEmittedPaint[XY(id().x, id().y, CalculationSize.x)] = result_paint;
        // log_float(deleted);
    }
    else
    {
        // remove previously written rakel mapped info
        Paint resetPaint;
        resetPaint.volume = 0;
        resetPaint.color = float4(0,0,0,0);
        RakelEmittedPaint[XY(id().x, id().y, CalculationSize.x)] = resetPaint;
    }
}

#pragma kernel main

[numthreads(32,1,1)]
void main (uint3 id_ : SV_DispatchThreadID)
{
    id__ = id_;
    if (is_relevant_thread(id(), CalculationSize))
    {
        // extract rakel mapped info from previous step
        Paint p = RakelEmittedPaint[XY(id().x, id().y, CalculationSize.x)];
        float2 rakel_reservoir_pixel = float2(p.color.x, p.color.y);
        float distance = p.color.z;

        // calculate pixel position on canvas
        // was uint2 before, but negative values are possible due to padding and no prevention of that because it should not cause any more problems
        int2 canvas_pixel = id() + CalculationPosition;
        
        emit(rakel_reservoir_pixel, uint2(ClipRadiusX, 1), distance, canvas_pixel);
    }
}