#include "../ShaderBase/shader_base.hlsl"

#include "../convert.hlsl"
#include "../volume.hlsl"
#include "../Paint/paint.hlsl"
#include "../math.hlsl"
#include "../sutherland_hodgman.hlsl"
#include "../rakel.hlsl"
#include "../mapped_info.hlsl"

uint TextureResolution;

RWStructuredBuffer<Rakel> RakelInfo;
RWStructuredBuffer<Paint> RakelApplicationReservoir;
RWStructuredBuffer<Paint> RakelPickupReservoir;
uint2 RakelReservoirSize;

float CanvasPositionZ;
RWStructuredBuffer<Paint> CanvasReservoir;
uint2 CanvasReservoirSize;
RWStructuredBuffer<MappedInfo> RakelMappedInfo;

int ClipRadiusX;
float RakelTilt_MAX;
float EmitDistance_MAX;
float EmitVolume_MIN;
float EmitVolume_MAX;
float EmitVolumeApplicationReservoirRate;
float EmitVolumePickupReservoirRate;

RWStructuredBuffer<float> DistortionMap;
uint2 DistortionMapSize;
uint DistortionMapIndex;

RWStructuredBuffer<Paint> RakelEmittedPaint;


// radius of 1 does an emit on a 3x3 area from emit source grid, based on the rounded rakel_reservoir_pixel
void emit(float2 rakel_reservoir_pixel, int2 radius, float distance, int2 canvas_pixel)
{
    int2 rakel_reservoir_pixel_nearest = round(rakel_reservoir_pixel);

    // TODO For more detail, this (and the volume_to_emit value) could also
    //      be calculated based on the polygon overlap
    float volume_rakel =
        RakelApplicationReservoir[XYZ(rakel_reservoir_pixel_nearest.x, rakel_reservoir_pixel_nearest.y, 1, RakelReservoirSize)].volume
        + RakelPickupReservoir[XYZ(rakel_reservoir_pixel_nearest.x, rakel_reservoir_pixel_nearest.y, 1, RakelReservoirSize)].volume;
    float volume_canvas = CanvasReservoir[XYZ(canvas_pixel.x, canvas_pixel.y, 1, CanvasReservoirSize)].volume;
    float paint_thickness_rakel = volume_rakel * VOLUME_THICKNESS();
    float paint_thickness_canvas = volume_canvas * VOLUME_THICKNESS();
    float surface_distance = distance - paint_thickness_rakel - paint_thickness_canvas;
    bool surfaces_touch = surface_distance <= 0;

    if (surfaces_touch)
    {
        Rakel rakel = RakelInfo[0];
        // float volume_to_emit = emit_volume(
        //     distance,
        //     EmitDistance_MAX,
        //     RakelTilt,
        //     RakelTilt_MAX,
        //     EmitVolume_MIN * PAINT_UNIT(),
        //     EmitVolume_MAX * PAINT_UNIT());

        // TODO adjust this, when implementing dried paint
        float rakel_edge_distance_from_canvas = abs(rakel.edge_z - CanvasPositionZ);
        float rakel_edge_distance_from_canvas_paint_surface = rakel_edge_distance_from_canvas - paint_thickness_canvas;
        float volume_fitting_between = max(rakel_edge_distance_from_canvas_paint_surface, 0) / VOLUME_THICKNESS();
        float volume_to_emit = volume_fitting_between + EmitVolume_MIN;

        //TODO there might be problems with rounding to negative indizes
        // VOLUME_TO_EMIT_TARGET_PU *= DistortionMap[XY(rakel_reservoir_pixel_nearest.x + DistortionMapIndex, rakel_reservoir_pixel_nearest.y, DistortionMapSize.x)];

        // Array size should really be [1+2*radius.y][1+2*radius.x] but we can't make dynamic arrays
        // in HLSL. So we do 9x9, which is enough for 79 degree tilt and more is not useful for emitting anyways.
        // Also, with 11x11 we would get the following warning:
        // Shader warning in 'EmitFromRakel': Program 'main', warning X4714: sum of temp registers and indexable temp registers times 32 threads exceeds the recommended total 16384.  Performance may be reduced at kernel main (on metal)
        float4 color_to_emit_app[9][9];
        float4 color_to_emit_pu[9][9];
        float volume_to_emit_app[9][9];
        float volume_to_emit_pu[9][9];
        float4 result_color_app = float4(0,0,0,0);
        float4 result_color_pu = float4(0,0,0,0);
        float result_volume_app = 0;
        float result_volume_pu = 0;
        int y, x; // declare loop variables here so the compiler won't complain about double declaration of y
        for (y=-radius.y; y<=radius.y; y++) {
            for (x=-radius.x; x<=radius.x; x++) {
                // initialize arrays
                uint i = y + radius.y;
                uint j = x + radius.x;
                volume_to_emit_app[i][j] = 0;
                volume_to_emit_pu[i][j] = 0;
                color_to_emit_app[i][j] = float4(0,0,0,0);
                color_to_emit_pu[i][j] = float4(0,0,0,0);

                // calculate part
                int2 coord = rakel_reservoir_pixel_nearest + int2(x, y);
                if (pixel_in_array_range(coord, RakelReservoirSize)) { // Prevent out of bounds access to reservoir. Also, if it's not in range, this overlap part is not (and must not be!) used anyways.
                    float2 rakel_anchor_index_space = rakel_anchor_to_index_space(rakel.anchor, TextureResolution);
                    float2 rect_rakel_reservoir_pixel = rotate_by_y_2D(rakel_reservoir_pixel, rakel.tilt, rakel_anchor_index_space);
                    float2 rect_coord = rotate_by_y_2D(coord, rakel.tilt, rakel_anchor_index_space);
                    Rectangle fragment_rectangle = create_rectangle(rect_rakel_reservoir_pixel, -rakel.rotation, 0);
                    Rectangle reservoir_rectangle = create_rectangle(rect_coord, 0, rakel.tilt);
                    float overlap = calculate_exact_overlap(fragment_rectangle, reservoir_rectangle);

                    if (overlap > 0) {
                        // get paint
                        uint2 coord_ = uint2(coord.x, coord.y);
                        Paint available_app = RakelApplicationReservoir[XYZ(coord_.x, coord_.y, 1, RakelReservoirSize)];
                        Paint available_pu = RakelPickupReservoir[XYZ(coord_.x, coord_.y, 1, RakelReservoirSize)];

                        // calculate volume
                        // .. prefer pickup reservoir with rate
                        float pu_reservoir_rate = EmitVolumePickupReservoirRate / (EmitVolumeApplicationReservoirRate + EmitVolumePickupReservoirRate);
                        float target_volume_pu = overlap * volume_to_emit * pu_reservoir_rate;
                        float really_available_volume_pu = overlap * available_pu.volume;
                        float volume_to_emit_pu_ = min(target_volume_pu, really_available_volume_pu);
                        // .. take rest from application reservoir
                        float target_volume_app = overlap * volume_to_emit - volume_to_emit_pu_;
                        float really_available_volume_app = overlap * available_app.volume;
                        // .. if this is not enough, try take rest from pickup reservoir
                        float app_diff = target_volume_app - really_available_volume_app;
                        if (app_diff > 0)
                        {
                            target_volume_pu += app_diff;
                        }

                        volume_to_emit_app[i][j] = min(target_volume_app, really_available_volume_app);
                        volume_to_emit_pu[i][j] = min(target_volume_pu, really_available_volume_pu);
                        result_volume_app += volume_to_emit_app[i][j];
                        result_volume_pu += volume_to_emit_pu[i][j];

                        color_to_emit_app[i][j] = available_app.color;
                        color_to_emit_pu[i][j] = available_pu.color;
                    }
                }
            }
        }

        // determine color based on emitted volumes
        // TODO use mix function
        for (y=-radius.y; y<=radius.y; y++) {
            for (x=-radius.x; x<=radius.x; x++) {
                int2 coord = rakel_reservoir_pixel_nearest + int2(x, y);
                if (pixel_in_array_range(coord, RakelReservoirSize)) {
                    uint i = y + radius.y;
                    uint j = x + radius.x;
                    float volume_part_app = volume_to_emit_app[i][j] / unzero(result_volume_app);
                    float volume_part_pu = volume_to_emit_pu[i][j] / unzero(result_volume_pu);
                    result_color_app += volume_part_app * color_to_emit_app[i][j];
                    result_color_pu += volume_part_pu * color_to_emit_pu[i][j];
                }
            }
        }

        Paint result_paint_app;
        Paint result_paint_pu;
        result_paint_app.color = result_color_app;
        result_paint_pu.color = result_color_pu;
        result_paint_app.volume = result_volume_app;
        result_paint_pu.volume = result_volume_pu;

        Paint result_paint = mix(result_paint_app, result_paint_pu);

        // delete amount of taken volume from reservoir (z=0)
        float deleted = 0;
        for (y=-radius.y; y<=radius.y; y++) {
            for (x=-radius.x; x<=radius.x; x++) {
                int2 coord = rakel_reservoir_pixel_nearest + int2(x, y);
                if (pixel_in_array_range(coord, RakelReservoirSize)) {
                    uint i = y + radius.y;
                    uint j = x + radius.x;
                    float to_be_deleted_app = volume_to_emit_app[i][j];
                    float to_be_deleted_pu = volume_to_emit_pu[i][j];

                    uint2 coord_ = uint2(coord.x, coord.y);
                    RakelApplicationReservoir[XYZ(coord_.x, coord_.y, 0, RakelReservoirSize)].volume -= to_be_deleted_app;
                    deleted += to_be_deleted_app;
                    RakelPickupReservoir[XYZ(coord_.x, coord_.y, 0, RakelReservoirSize)].volume -= to_be_deleted_pu;
                }
            }
        }

        RakelEmittedPaint[XY(id().x, id().y, CalculationSize.x)] = result_paint;
        // log_float(deleted);
    }
}

#pragma kernel main

[numthreads(32,1,1)]
void main (uint3 id_ : SV_DispatchThreadID)
{
    id__ = id_;
    if (is_relevant_thread(id(), CalculationSize))
    {
        // calculate pixel position on canvas
        // was uint2 before, but negative values are possible due to padding and no prevention of that because it should not cause any more problems
        int2 canvas_pixel = id() + CalculationPosition;

        MappedInfo rakel_mapped_info = RakelMappedInfo[XY(id().x, id().y, CalculationSize.x)];
        emit(rakel_mapped_info.reservoir_pixel, uint2(ClipRadiusX, 1), rakel_mapped_info.distance, canvas_pixel);
    }
}